```sql
declare
    v_sal scott.employees.salary%type;
    v_empid scott.employees.employee_id%type;
    cursor emp_sal_cursor is select salary, employee_id from scott.employees where department_id = 80;
begin
    open emp_sal_cursor;
    fetch emp_sal_cursor into v_sal, v_empid;
    while emp_sal_cursor%found loop
        dbms_output.put_line(v_sal ||' '||v_empid);
        fetch emp_sal_cursor into v_sal, v_empid;
    end loop;
    close emp_sal_cursor;
end;
```





```sql
declare
    cursor v_sal_cursor(sal number) is select salary, employee_id from scott.employees where salary > sal;
begin
    for c in v_sal_cursor(8000) loop
        dbms_output.put_line(c.employee_id ||', ' ||c.salary);
    end loop;
end;
```



- cursor 是一种类型, 所以可以定义变量



# 游标的概念: 

   游标是SQL的一个内存工作区，由系统或用户以变量的形式定义。游标的作用就是用于临时存储从数据库中提取的数据块。在某些情况下，需要把数据从存放在磁盘的表中调到计算机内存中进行处理，最后将处理结果显示出来或最终写回数据库。这样数据处理的速度才会提高，否则频繁的磁盘数据交换会降低效率。 

# 游标有两种类型：

显式游标和隐式游标。在前述程序中用到的SELECT...INTO...查询语句，一次只能从数据库中提取一行数据，对于这种形式的查询和DML操作，系统都会使用一个隐式游标。但是如果要提取多行数据，就要由程序员定义一个显式游标，并通过与游标有关的语句进行处理。显式游标对应一个返回结果为多行多列的SELECT语句。 
游标一旦打开，数据就从数据库中传送到游标变量中，然后应用程序再从游标变量中分解出需要的数据，并进行处理。 



> references:
>
> https://www.cnblogs.com/xiaoliu66007/p/7495753.html
>
> https://blog.csdn.net/liyong199012/article/details/8948952
>
> https://www.cnblogs.com/liuqiyun/p/6552700.html => https://blog.51cto.com/589985/1054297 (这个是正确的例子)



```sql
-- conn scott/tiger
Declare 
    Type MyRefCurA IS  REF CURSOR RETURN emp%RowType;
	vRefCurA  MyRefCurA;
	vTempA  vRefCurA%RowType;
Begin
    Open  vRefCurA  For Select  *  from   emp   Where  SAL > 2000;
	Loop
    	Fetch  vRefCurA InTo  vTempA;
		Exit  When  vRefCurA%NotFound;
		DBMS_OUTPUT.PUT_LINE(vRefCurA%RowCount||'  ' ||'  '||vTempA.ename ||'  '||vTempA.sal);
	End Loop;
	Close vRefCurA;
End;
```

记录类型:

> references: https://blog.csdn.net/m0_51330854/article/details/114838461





- 关于通过记录来进行ref cursor的获取

```sql
 PROCEDURE sp_get_his_cparty_amt_limit
  (
    i_operator   IN pt.toper,     -- 操作员代码
    i_address    IN pt.taddress,  -- 操作员地址
    i_func_no    IN pt.tfuncno,   -- 功能编号
    i_trade_date IN pt.tsdate,    -- 交易日期
    o_errcode    OUT pt.terrcode, -- 错误代码
    o_errmsg     OUT pt.terrmsg   -- 错误信息
  ) AS
    v_trade_date pt.tsdate;
    v_cursor1    pt.tcursor;
    TYPE tmp_data IS RECORD(
      counterparty   clear.tc_his_cparty_amt_limit.counterparty%TYPE,
      cp_abbr        clear.tc_his_cparty_amt_limit.cp_abbr%TYPE,
      currency       clear.tc_his_cparty_amt_limit.currency%TYPE,
      trade_type     clear.tc_his_cparty_amt_limit.trade_type%TYPE,
      end_date       clear.tc_his_cparty_amt_limit.end_date%TYPE,
      cp_name        clear.tc_his_cparty_amt_limit.cp_name%TYPE,
      limit_amt      pt.tstr,  --20190530修改，因为前台展示有新需求，此字段to_char展示
      used_amt       clear.tc_his_cparty_amt_limit.used_amt%TYPE,
      overtime_amt   clear.tc_his_cparty_amt_limit.overtime_amt%TYPE,
      remain_amt     pt.tstr,
      creditHedging_amt    pt.tstr,   --含套保拟用信金额
      credit_amt    pt.tstr,                 --无套保拟用信金额
      creditedHedging_amt  pt.tstr,  --含套保已用信金额
      credited_amt  pt.tstr,               --无套保已用信金额
      pro_use_amt  pt.tstr,
      available_amt   pt.tstr,
      limit_amt_rate clear.tc_counterparty_amt_limit.limit_amt_rate%TYPE,
      replay_date   clear.tc_counterparty_amt_limit.replay_date%TYPE,
      limit_source   clear.tc_counterparty_amt_limit.limit_source%TYPE,
      modify_date   clear.tc_counterparty_amt_limit.modify_date%TYPE
      );
    v_cursor2    tmp_data;
    v_errcode    pt.terrcode;
    v_errmsg     pt.terrmsg;
  BEGIN
    o_errcode    := pc.cn_err_code_init;
    o_errmsg     := pc.cn_err_msg_init;  
    v_trade_date := i_trade_date;
    
    --支持重复结算
    DELETE FROM clear.tc_his_cparty_amt_limit WHERE trade_date = v_trade_date;           
    clear.pkc_foreign_database_manage.sp_qry_counterparty_amt_limit(i_operator => null,
                                                                    i_address  => null,
                                                                    i_func_no  => null,
                                                                    i_counterparty_no   => null,
                                                                    i_counterparty_abbr => null,
                                                                    i_trade_type => null,
                                                                    i_currency   => null,
                                                                    o_cursor  => v_cursor1,
                                                                    o_errcode => v_errcode,
                                                                    o_errmsg  => v_errmsg);
    IF (v_errcode <> 0) THEN
      o_errcode := 1;
      o_errmsg  := '上海农商行获取交易对手方限额数据出错' || v_errmsg;
      GOTO l_exit;
    END IF;
    
    IF v_cursor1%ROWCOUNT <> 0 THEN
		LOOP
		  FETCH v_cursor1 INTO v_cursor2;
		  EXIT WHEN v_cursor1%NOTFOUND;
		  INSERT INTO clear.tc_his_cparty_amt_limit(
				 trade_date,    --统计日期
				 counterparty,  --对手方代码
				 cp_abbr,       --对手方简称
				 currency,      --币种：见tp_public_dict的036
				 trade_type,    --类型：见tp_public_dict的280
				 end_date,      --到期日
				 cp_name,       --对手方名称
				 limit_amt,     --限额
				 used_amt,      --已用额度
				 overtime_amt,  --超期占用额度
				 remain_amt,    --剩余额度
				 creditedHedging_amt, --已用信含套保
				 credited_amt    --已用信无套保
				 )    
		  SELECT v_trade_date,            --统计日期
				 v_cursor2.counterparty,  --对手方代码
				 v_cursor2.cp_abbr,       --对手方简称
				 v_cursor2.currency,      --币种：见tp_public_dict的036
				 v_cursor2.trade_type,    --类型：见tp_public_dict的280
				 v_cursor2.end_date,      --到期日
				 v_cursor2.cp_name,       --对手方名称
				 to_number(v_cursor2.limit_amt,'999,999,999,999.999'),     --限额
				 v_cursor2.used_amt,      --已用额度
				 v_cursor2.overtime_amt,  --超期占用额度
				 to_number(v_cursor2.remain_amt,'999,999,999,999.999'),     --剩余额度
				 v_cursor2.creditedHedging_amt, --已用信含套保
				 v_cursor2.credited_amt --已用信无套保
			FROM dual;
		END LOOP;
	END IF;
    COMMIT;
  
    --  写系统操作日志
    o_errcode := 0;
    o_errmsg  := '获取交易对手方限额数据成功';
  
    --  plog.debug(i_operator,i_address,i_func_no,o_errmsg);
    RETURN;
  
    <<l_exit>>
    --  失败信息写入系统日志
    plog.error(i_operator, i_address, i_func_no, o_errcode, o_errmsg);
    RETURN;
  
  EXCEPTION
    WHEN OTHERS THEN
      plog.fatal(i_operator, i_address, i_func_no, o_errcode, o_errmsg);
      RETURN;
  END sp_get_his_cparty_amt_limit;
```





e.g.

```sql
declare
    i_operator  pt.toper    default null;
    i_address   pt.taddress default null;
    i_func_no   pt.tfuncno  default null;
    i_counterparty_no   clear.tc_counterparty_amt_limit.counterparty_no%TYPE default '0';
    i_counterparty_abbr clear.tc_counterparty_amt_limit.counterparty_abbr%TYPE default '0';
    v_amt_limit pt.tcursor;
    v_daily_storage pt.tcursor;
    o_errcode   pt.terrcode;
    v_errmsg    pt.terrmsg;
    tmp_counterparty pt.tstr;
    tmp_cp_abbr      pt.tstr;
    tmp_currency     pt.tstr;
    tmp_trade_type   pt.tstr;
    tmp_end_date     pt.tstr;
    tmp_cp_name      pt.tstr;
    tmp_limit_amt    pt.tstr;
    tmp_used_amt     pt.tstr;   
    tmp_overtime_amt pt.tstr;
    tmp_remain_at    pt.tstr; 
    tmp_creditHedging_amt   pt.tstr; 
    tmp_credit_amt          pt.tstr; 
    tmp_creditedHedging_amt pt.tstr; 
    tmp_credited_amt        pt.tstr; 
    tmp_pro_use             pt.tstr;
    tmp_limit_amt_rate  pt.tstr;
    tmp_replay_date    pt.tstr;
    tmp_limit_source   pt.tstr;
    tmp_modify_date    pt.tstr;
    tmp_available_amt   pt.tstr;
    tmp_limit_status   pt.tstr;
begin
   clear.pkc_foreign_database_manage.sp_qry_counterparty_amt_limit(i_operator,
                                                                    i_address,
                                                                    i_func_no,
                                                                    TRIM(i_counterparty_no),
                                                                    TRIM(i_counterparty_abbr),
                                                                    pc.cn_cp_total,
                                                                    '0',
                                                                    v_amt_limit,
                                                                    o_errcode, 
                                                                    v_errmsg); 
                                                                    
     DBMS_OUTPUT.PUT_LINE(v_amt_limit%ROWCOUNT);
                                                                        
     FETCH v_amt_limit INTO  
              tmp_counterparty,
              tmp_cp_abbr,
              tmp_currency,
              tmp_trade_type,
              tmp_end_date,
              tmp_cp_name,
              tmp_limit_amt,
              tmp_used_amt,   
              tmp_overtime_amt,
              tmp_remain_at,
              tmp_creditHedging_amt,
              tmp_credit_amt, 
              tmp_creditedHedging_amt,
              tmp_credited_amt,
              tmp_pro_use,
              tmp_available_amt,
              tmp_limit_amt_rate,
              tmp_replay_date,
              tmp_limit_source,
              tmp_modify_date,
              tmp_limit_status;  
                                 
      IF v_amt_limit%NOTFOUND THEN
            dbms_output.put_line('hhhhh');
      END IF;          
end;
```

